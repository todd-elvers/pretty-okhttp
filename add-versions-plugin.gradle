/**
 * This file adds the 'Versions' plugin to our project so we can lookup the latest 
 * versions of our dependencies using the :dependencyUpdates.
 */


// Required syntax for applying a non-standard plugin from outside of build.gradle
buildscript {
    repositories.jcenter()
    dependencies.classpath "com.github.ben-manes:gradle-versions-plugin:0.21.0"
}
apply plugin: com.github.benmanes.gradle.versions.VersionsPlugin


// Configure the :dependencyUpdates task to only output release versions
def suffixesOfVersionsToOmit = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea']
dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean isNonReleaseVersion = suffixesOfVersionsToOmit.any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*.*/
            }

            if (isNonReleaseVersion) {
                selection.reject('Release candidate')
            }
        }
    }
}